#compdef xcodebuild

_archs() {
  local -a archs
  archs=$(xcodebuild -showBuildSettings 2>/dev/null | grep VALID_ARCHS \
    | cut -f 2 -d '=')
  echo $archs
}

_configurations() {
  local -a configurations
  configurations=$(xcodebuild -list 2>/dev/null \
    | sed -n -e '/Build Configurations:/,/If no build/p' \
    | sed -e '1d;$d' -e 's/^[ \t]*//')
  echo $configurations
}

_schemes() {
  local -a schemes
  schemes=$(xcodebuild -list 2>/dev/null \
    | sed -e '1,/Schemes:/d' -e 's/^[ \t]*//')
  echo $schemes
}

_sdks() {
  local -a sdks
  sdks=$(xcodebuild -showsdks | grep -o "\-sdk.*" | cut -f 2 -d ' ')
  echo $sdks
}

_targets() {
  local -a targets
  targets=$(xcodebuild -list 2>/dev/null \
    | sed -n -e '/Targets:/,/Build Configurations:/p' \
    | sed -e '1d;$d' -e 's/^[ \t]*//')
  echo $targets
}

_actions() {
  local -a actions
  actions=( \
    "analyze:Build and analyze a target or scheme" \
    "archive:Archive a scheme" \
    "build:Build the target" \
    "clean:Remove build products and intermediate files" \
    "install:Build and install the target" \
    "installsrc:Copy the source of the project" \
    "test:Test a scheme from" \
    )

  _describe "Build action" actions
}

_arguments \
  '(-n -dry-run)'{-n,-dry-run}'[Print but do not execute commands]' \
  '*: :->actions' \
  '-alltargets[Build all targets in the specified project]' \
  '-arch[Architecture to build]:Architecture to build:->archs' \
  '-archivePath[Archive Path]:Archive Path:_directories' \
  '-configuration[Build configuration to use]:Build configuration to use:->configurations' \
  '-derivedDataPath[Overrides derived data directory]:Overrides derived data directory:_directories' \
  '-destination-timeout[Device search timeout]:Device search timeout' \
  '-destination[Device destination settings]:Device destination settings:' \
  '-enableAddressSanitizer[Turn the address sanitizer on/off]:enabled:(YES NO)' \
  '-enableThreadSanitizer[Turn the thread sanitizer on/off]:enabled:(YES NO)' \
  '-enableUndefinedBehaviorSanitizer[Turn the UB sanitizer on/off]:enabled:(YES NO)' \
  '-enableCodeCoverage[Turn code coverage on/off]:enabled:(YES NO)' \
  '-exportArchive[Exports an archive]' \
  '-exportFormat[Format for archive]:Format for archive:(IPA PKG APP)' \
  '-exportInstallerIdentity[Installer identity for archiving]:Installer identity for archiving' \
  '-exportProvisiningProfile[Provisioning profile for archiving]:Provisioning profile for archiving' \
  '-exportSigningIdentity[Signing identity for archiving]:Signing identity for archiving' \
  '-exportWithOriginalSigningIdentity[Specifies signing identity for archiving]' \
  '-list[Lists targets configurations and schemes]' \
  '-project[xcodeproj file]:xcodeproj:->projects' \
  '-resultBundlePath[Result bundle path]:Result bundle path:_directories' \
  '-scheme[Scheme to build]:Scheme to build:->schemes' \
  '-sdk[SDK to build with]:SDK to build with:->sdks' \
  '-showBuildSettings[Print project build settings]' \
  '-showsdks[List all SDK versions]' \
  '-skipUnavailableActions[Skip options that would fail]' \
  '-target[Target to build]:Target to build:->targets' \
  '-usage[Display xcodebuild usage]' \
  '-version[Prints Xcode version]' \
  '-workspace[xcworkspace file]:xcworkspace:->workspaces' \
  '-xcconfig[xcconfig file]:xcconfig:->xcconfigs'

case "$state" in
  actions)
    _actions
    ;;
  archs)
    archs=($(_archs))
    if [[ $archs != "" ]]; then
      _values "Architecture to build" $archs
    fi
    ;;
  configurations)
    IFS=$'\n' configurations=($(_configurations))
    if [[ $configurations != "" ]]; then
      _values "Build configuration to use" $configurations
    fi
    ;;
  projects)
    local -a projects
    projects=(*.xcodeproj)
    _multi_parts / projects
    ;;
  schemes)
    IFS=$'\n' schemes=($(_schemes))
    if [[ $schemes != "" ]]; then
      _values "Scheme to build" $schemes
    fi
    ;;
  sdks)
    if ([[ ${+_installed_sdks} -eq 0 ]] || _cache_invalid SDKS) && ! _retrieve_cache SDKS; then
      _installed_sdks=($(_sdks))
      _store_cache SDKS _installed_sdks
    fi

    _values "SDK to build with" $_installed_sdks
    ;;
  targets)
    IFS=$'\n' targets=($(_targets))
    if [[ $targets != "" ]]; then
      _values "Target to build" $targets
    fi
    ;;
  workspaces)
    local -a workspaces
    workspaces=(*.xcworkspace)
    _multi_parts / workspaces
    ;;
  xcconfigs)
    local -a xcconfigs
    xcconfigs=(*.xcconfig)
    _multi_parts / xcconfigs
    ;;
esac
