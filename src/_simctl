#compdef simctl

_simctl() {
  _arguments \
    '--set: :_files -/' \
    '--profiles: :_files -/' \
    ': :_commands' \
    '*:: :_options'
}

_commands() {
  local -a commands
  commands=(
    'help:Prints the usage for a given subcommand.'
    'create:Create a new device.'
  )
  _describe subcommand commands
}

_options() {
  case $line[1] in

    (help)
      _arguments ': :_commands'
      ;;

    (create)
      _arguments \
        ':name:' \
        ':device type id:_devicetypes' \
        '::runtime id:{DEVICETYPE=${(Q)words[3]} _runtimes}'
      ;;

  esac
}

_devicetypes() {
  local -a devicetypes
  devicetypes=(${(f)"$(xcrun simctl list devicetypes -j | jq -r '.devicetypes[].name')"})
  _describe devicetype devicetypes
}

_runtimes() {
  local -a runtimes
  runtimes=(${(f)"$(
    jq -nr \
      --argfile devicetypes <(xcrun simctl list devicetypes "${DEVICETYPE:-.}" -j) \
      --argfile runtimes <(xcrun simctl list runtimes -j) \
      '$runtimes.runtimes[] | select(any(.supportedDeviceTypes[].identifier == $devicetypes.devicetypes[].identifier; .)).identifier'
    )"})
  _describe runtime runtimes
}
